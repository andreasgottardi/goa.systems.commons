plugins {
	id 'java-library'
	id 'maven-publish'
}

ext {	
	junitvers = '5.7.1'
	junitplatvers = '1.7.1' 
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '0.0.1'

dependencies {

	testImplementation 'org.junit.jupiter:junit-jupiter-api:' + junitvers
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + junitvers
	testRuntimeOnly 'org.junit.platform:junit-platform-commons:' + junitplatvers

	implementation 'org.slf4j:slf4j-api:1.7.30'
	testRuntimeOnly 'ch.qos.logback:logback-core:1.2.3'
	testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
}

test {
	useJUnitPlatform()
}

repositories {

	jcenter()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
	repositories {
        maven {
            url = REPO_DIR
        }
    }
    publications {
        mavenJava(MavenPublication) {
        	groupId = 'goa.systems'
        	artifactId = 'commons'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'GOA systems commons'
                description = 'Library containing common logic.'
                url = 'https://goa.systems/commons'
                licenses {
                    license {
                        name = 'GPLv3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'goa'
                        name = 'Andreas Gottardi'
                        email = 'goa@goa.systems'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:andreasgottardi/goa.systems.commons.git'
                    url = 'https://github.com/goa.systems.commons'
                }
            }
        }
    }
    println "Publication done"
}

/*  This task exports the generated jar files into the directory "build/remote".
    Build tasks can grab them from there and handle them as needed. */
task localPublish (group: 'publishing', description: 'Exports all jar files to publish') {
	
	dependsOn publish
	mustRunAfter publish
    doLast {
	    println "Starting local publish"
		copy {
			from(fileTree(REPO_DIR) { includes }.files) {
		        include '**/*.jar'
		    }
		    into 'build/remote'
	    }
	}
}